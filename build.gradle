plugins {
    id 'java'
    id 'application'
    id 'io.qameta.allure' version '2.9.6'
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

group = 'com.ta'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    agent "org.aspectj:aspectjweaver:$aspectJVer"
    implementation "io.qameta.allure:allure-assertj:${allureVer}"
    implementation "io.appium:java-client:${appiumJavaVer}"
    implementation "org.apache.commons:commons-lang3:${apacheCommonVer}"
    implementation "io.github.bonigarcia:webdrivermanager:${driverManagerVer}"
    testImplementation "org.seleniumhq.selenium:selenium-java:${seleniumVer}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVer}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVer}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVer}"
    testImplementation "io.qameta.allure:allure-junit5:${allureVer}"
}

allure {
    version = '2.13.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}
